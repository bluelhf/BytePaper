plugins {
    id("java-library")
    id("xyz.jpenilla.run-paper") version "1.0.6"
    id("io.papermc.paperweight.userdev") version "1.5.5"
    id("com.github.johnrengelman.shadow") version "7.1.2"
    id("net.minecrell.plugin-yml.bukkit") version "0.5.2"
}

import java.nio.charset.Charset
import java.nio.charset.StandardCharsets

repositories {
    gradlePluginPortal()
    maven { url = uri("https://libraries.minecraft.net/") }
    maven { url = uri("https://maven.fabricmc.net/") }
    maven { url = uri("https://papermc.io/repo/repository/maven-public/") }
    maven { url = uri("https://repo.kenzie.mx/releases") }
    maven { url = uri("https://gitlab.com/api/v4/projects/18568066/packages/maven") }
}

group = 'blue.lhf'
version = '0.4.0'

int javaVersion = 17
String author = "bluelhf"
Charset charset = StandardCharsets.UTF_8
def ver = [
        major   : "1",
        minor   : "20",
        patch   : "1",
        revision: "0.1",
        tag     : "SNAPSHOT"
]


String serverImpl = "${ver.major}.${ver.minor}"
if (ver.patch != null) serverImpl += "." + ver.patch
if (ver.revision != null) serverImpl += "-R" + ver.revision
if (ver.tag != null) serverImpl += "-" + ver.tag

java {
    toolchain.languageVersion.set(JavaLanguageVersion.of(javaVersion))
}

dependencies {
    implementation 'org.reflections:reflections:0.10.2'
    implementation "org.byteskript:byteskript:1.0.39"
    paperweightDevelopmentBundle("io.papermc.paper:dev-bundle:${serverImpl}")
    implementation "com.moderocky:Commander:3.0.0-SNAPSHOT"
}


tasks {
    assemble {
        dependsOn(reobfJar)
    }

    compileJava {
        compileJava.options.encoding = charset.name()
        options.release.set(javaVersion)
    }

    javadoc {
        options.encoding = charset.name()
    }

    processResources {
        filteringCharset = charset.name()
    }
}

bukkit {
    main = "${group}.${project.name.toLowerCase()}.BytePaper"
    apiVersion = "${ver.major}.${ver.minor}"
    authors = [author]

    commands {
        bytepaper {
            aliases = ["byteskript", "bp", "bsk"]
            description = "Manage BytePaper"
            permission = "bp.manage"
        }
    }

    permissions {
        "bp.*" {
            children = ["bp.manage"]
        }
        "bp.manage" {
            description = "Allows you to use the BytePaper management command"
            setDefault("OP")
        }
    }
}